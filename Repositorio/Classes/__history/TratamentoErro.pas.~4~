unit TratamentoErro;

interface

uses
  DBClient, SysUtils;

type
  TTratamentoErro = class
  private
    class var FMensagemErroApplyUpdate: String;
  public
    class procedure HandleReconcileError(MensagemErro: String;
      var Acao: TReconcileAction);
    class function Execute(MensagemErro: String): String;
    class function GetMensagemErroApplyUpdate: String;
  end;

var
  TratarErro: TTratamentoErro;

implementation

{ TTratamentoErro }

class procedure TTratamentoErro.HandleReconcileError(MensagemErro: String;
  var Acao: TReconcileAction);
var
  PosicaoInicial: Integer;
  PosicaoFinal: Integer;
begin
  // Ação que o ClientDataSet irá executar.
  Acao := raAbort;

  // Mensagem de erro original, não personalizada.
  FMensagemErroApplyUpdate := MensagemErro;

  // -----------
  // Primary Key
  // -----------
  if Pos('PRIMARY OR UNIQUE KEY', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('PK_', UpperCase(MensagemErro));

    if (PosicaoInicial = 0) then
    begin
      PosicaoInicial := Pos('UNQ_', UpperCase(MensagemErro));
    end;

    PosicaoFinal := Pos('ON TABLE', UpperCase(MensagemErro)) - 2;

    FMensagemErroApplyUpdate := 'Violação de chave primária. ' +
      Copy(MensagemErro, PosicaoInicial, (PosicaoFinal - PosicaoInicial));
  end

  // -----------
  // Foreing Key
  // -----------
  else if Pos('FOREIGN KEY', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('FK_', UpperCase(MensagemErro));
    PosicaoFinal := Pos('ON TABLE', UpperCase(MensagemErro)) - 2;

    FMensagemErroApplyUpdate := 'Violação de chave estrangeira. ' +
      Copy(MensagemErro, PosicaoInicial, (PosicaoFinal - PosicaoInicial));
  end

  // -----
  // Check
  // -----
  else if Pos('CHECK', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('CHK_', UpperCase(MensagemErro));
    PosicaoFinal := Pos('ON VIEW', UpperCase(MensagemErro)) - 1;

    FMensagemErroApplyUpdate := 'Violação de integridade. ' +
      Copy(MensagemErro, PosicaoInicial, (PosicaoFinal - PosicaoInicial));
  end

  // ---------------------------------
  // Multiple Rows in Singleton Select
  // ---------------------------------
  else if Pos('MULTIPLE ROWS', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('AT TRIGGER', UpperCase(MensagemErro)) + 11;
    PosicaoFinal := Pos('LINE', UpperCase(MensagemErro)) - 1;
    FMensagemErroApplyUpdate := 'Múltiplas linhas no mesmo select. ' +
      Copy(MensagemErro, PosicaoInicial, (PosicaoFinal - PosicaoInicial));
  end

  // -----------------------------------------------------------
  // Arithmetic Exception / Numeric Overflow / String Truncation
  // -----------------------------------------------------------
  else if (Pos('ARITHMETIC EXCEPTION', UpperCase(MensagemErro)) > 0) or
    (Pos('NUMERIC OVERFLOW', UpperCase(MensagemErro)) > 0) or
    (Pos('STRING TRUNCATION', UpperCase(MensagemErro)) > 0) then
  begin
    PosicaoInicial := Pos('AT TRIGGER', UpperCase(MensagemErro)) + 11;
    PosicaoFinal := Pos('LINE', UpperCase(MensagemErro)) - 1;
    FMensagemErroApplyUpdate := 'Erro na estrutura dos dados. ' +
      Copy(MensagemErro, PosicaoInicial, (PosicaoFinal - PosicaoInicial));
  end

  // ---------------------------
  // Exception Default [Trigger]
  // ---------------------------
  else if Pos('E_EXCEPTION_DEFAULT', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('E_EXCEPTION_DEFAULT', UpperCase(MensagemErro)) + 20;
    PosicaoFinal := Pos('AT TRIGGER', UpperCase(MensagemErro)) - 1;

    FMensagemErroApplyUpdate := Copy(MensagemErro, PosicaoInicial,
      (PosicaoFinal - PosicaoInicial));
  end

  // ------------
  // Unique Index
  // ------------
  else if Pos('UNIQUE INDEX', UpperCase(MensagemErro)) > 0 then
  begin
    PosicaoInicial := Pos('UNIQUE INDEX', UpperCase(MensagemErro)) + 13;
    PosicaoFinal := Length(MensagemErro) - PosicaoInicial + 1;

    FMensagemErroApplyUpdate := 'Violação de índice único. ' + sLineBreak +
      sLineBreak + 'Índice: ' + Copy(MensagemErro, PosicaoInicial,
      PosicaoFinal);
  end

  // -------------
  // Lock Conflict
  // -------------
  else if Pos('LOCK CONFLICT', UpperCase(MensagemErro)) > 0 then
  begin
    FMensagemErroApplyUpdate := 'Outro usuário alterando o registro.';
  end;
end;

class function TTratamentoErro.Execute(MensagemErro: String): String;
var
  VAction: TReconcileAction;
begin
  Self.HandleReconcileError(MensagemErro, VAction);
  Result := Self.GetMensagemErroApplyUpdate();
end;

class function TTratamentoErro.GetMensagemErroApplyUpdate: String;
begin
  Result := Self.FMensagemErroApplyUpdate;
end;

end.
